apply plugin: 'java'
apply plugin: 'eclipse'


dependencies {
   compile project(':ILCommons')

   compile 'org.apache.hadoop:hadoop-core:1.0.3'

   compile fileTree(dir: 'libs', include: '*.jar')   

   //get us core, tdb, arq, and iri
   compile 'org.apache.jena:apache-jena:2.7.3'

   //gets a lot of sesame jars   
   compile 'org.openrdf.sesame:sesame-runtime:2.6.6'

   compile 'javax.servlet:servlet-api:2.5'
   compile 'junit:junit:4.9'  

   compile 'org.apache.httpcomponents:httpmime:4.2.2'
             
   compile 'org.apache.solr:solr-solrj:4.1.0'
   
   compile 'com.google.guava:guava:14.0'            
}

task list(dependsOn: configurations.compile) << { 
    println "classpath = ${configurations.compile.collect {File file -> file.name}}"
}

task( step8ConfirmSolrIndexCompleteness, dependsOn: 'assemble', type: JavaExec) {
 group="Incremental Update Steps"
 description = "Generates a report of what records are missing\
 from the Solr Index or are in the Solr index but not in Voyager."

 main = 'edu.cornell.library.integration.indexer.updates.ConfirmSolrIndexCompleteness'
 classpath = sourceSets.main.runtimeClasspath
 args = [ 'http://da-stg-msolr.library.cornell.edu:8080/solr/August1' ]
}

task(step7DeleteFromSolr, dependsOn: 'assemble', type: JavaExec) {
 group="Incremental Update Steps" 
 description = 'Deletes the Bib Records identified in step 2 from the Solr Index.'

 main = 'edu.cornell.library.integration.delete.DeleteFromSolr'
 classpath = sourceSets.main.runtimeClasspath
 args = [ 'http://da-stg-msolr.library.cornell.edu:8080/solr/August1' ]
}

task(step6IndexDirectoryCmd, dependsOn: 'assemble', type: JavaExec) {
 group="Incremental Update Steps"
 description = "Builds Solr Documents for MARC\
 n-Triples from step 5 and adds them to the Solr index."
 maxHeapSize = '2048m'
 main = 'edu.cornell.library.integration.indexer.IndexDirectoryCmd'
 classpath = sourceSets.main.runtimeClasspath
 args = [ 'http://da-stg-msolr.library.cornell.edu:8080/solr/August1',
          'http://culdata.library.cornell.edu/data/voyager/bib/bib.nt.daily' ]

 //systemProperty 'simple.message', 'Hello '
}

task libJar(type: Jar){
    description = 'Create a jar of all the dependencies of this project, but no classes from this project'
    from {
        configurations.compile.collect{ it.isDirectory() ? it : zipTree(it) }
    }
    archiveName 'rdfToSolr.dependencies.jar'
    // some how we are getting two slf4j implementations, 
    // some project's pom.xml is messed up.
    exclude "**/slf4j-jdk14*"
}

jar {
    description = 'Create a jar of all the dependencies and classes of this project'
    dependsOn configurations.compile
    from { 
       configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    }
    exclude "**/slf4j-jdk14*"
}

task copyToLib(type: Copy) {
    description = "Copy all dependencies to $buildDir/output/lib for use by IDEs like eclipse"  
    into "$buildDir/output/lib"
    from configurations.runtime
}


task (step2IdentifyDeletedRecords, dependsOn:'assemble', type: JavaExec){
 group="Incremental Update Steps"
 description="Gets a list of BIB and MFHD\
 records updates from the Voyager database for the previous day"

 main='edu.cornell.library.integration.indexer.updates.IdentifyDeletedRecords'
 classpath = sourceSets.main.runtimeClasspath
 maxHeapSize = '1024m'
}
